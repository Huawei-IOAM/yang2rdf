#
# Copyright (c) Huawei Technologies Co., Ltd. 2020-2024. All rights reserved.
#
# Licensed under the Apache License, Version 2.0 (the "License");
# you may not use this file except in compliance with the License.
# You may obtain a copy of the License at
#
#     http://www.apache.org/licenses/LICENSE-2.0
#
# Unless required by applicable law or agreed to in writing, software
# distributed under the License is distributed on an "AS IS" BASIS,
# WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
# See the License for the specific language governing permissions and
# limitations under the License.
#
PREFIX yang: <http://www.typefox.io/yang/Yang#>
PREFIX yang-aug: <http://www.typefox.io/yang/Yang#Augment/>
PREFIX yang-sn: <http://www.typefox.io/yang/Yang#SchemaNode/>
PREFIX yang-sni: <http://www.typefox.io/yang/Yang#SchemaNodeIdentifier/>
PREFIX yang-stmt: <http://www.typefox.io/yang/Yang#Statement/>
PREFIX yang2: <http://huawei.com/kg/def/yang/>

CONSTRUCT {
  ?target_stmt_iri a ?target_stmt_type_iri .
  ?target_stmt_iri yang2:hasSource ?stmt_iri .
  ?target_aug_stmt_iri yang2:contains ?target_stmt_iri
}
WHERE {
  ?_parent_stmt_iri yang-stmt:substatements ?augment_iri .
  ?augment_iri a yang:Augment .
  ?augment_iri yang-stmt:substatements ?stmt_iri .
  ?stmt_iri a ?stmt_type_iri .
  ?stmt_iri yang-sn:name ?stmt_name .
  ?augment_iri yang-aug:path/yang-sni:schemaNode ?aug_stmt_iri .
  ?target_aug_stmt_iri yang2:hasSource ?aug_stmt_iri .
  BIND(STRAFTER(STR(?stmt_type_iri), "#") AS ?stmt_type)
  BIND(IRI(CONCAT("http://huawei.com/kg/def/yang/", ?stmt_type)) AS ?target_stmt_type_iri)
  BIND(IRI(CONCAT(STR(?target_aug_stmt_iri), "/", ?_module_name, ':', ?stmt_name)) AS ?target_stmt_iri)
}
